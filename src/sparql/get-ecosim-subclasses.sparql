# SPARQL query to get all subclasses of ECOSIM classes
# and their associated properties
# Thanks to Mark Miller for this
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?class
(GROUP_CONCAT(DISTINCT ?has_unit_label;
        separator="|") AS ?has_units)
(GROUP_CONCAT(DISTINCT ?qualifier_label;
        separator="|") AS ?qualifiers)
(GROUP_CONCAT(DISTINCT ?attribute_label;
        separator="|") AS ?attributes)
(GROUP_CONCAT(DISTINCT ?measured_in_label;
        separator="|") AS ?measured_ins)
(GROUP_CONCAT(DISTINCT ?measurement_of_label;
        separator="|") AS ?measurement_ofs)
(GROUP_CONCAT(DISTINCT ?context_label;
        separator="|") AS ?contexts)
(GROUP_CONCAT(DISTINCT ?parent_label;
        separator="|") AS ?parents)
WHERE {
    ?class a owl:Class .
    OPTIONAL {
        ?class rdfs:label ?class_label 
    }
    OPTIONAL {
        ?class rdfs:subClassOf ?r1 .
        ?r1 a owl:Restriction ;
        owl:onProperty <http://purl.obolibrary.org/obo/ECOSIM#_has_unit> ;
        owl:someValuesFrom ?has_unit .
        OPTIONAL {
            ?has_unit rdfs:label ?raw_has_unit_label 
            BIND(
                IF(
                    STRSTARTS(STR(?has_unit), "http://purl.obolibrary.org/obo/ECOSIMCONCEPT_"),
                    CONCAT("ECOSIMCONCEPT:", REPLACE(STR(?has_unit), "^.*ECOSIMCONCEPT_", "")),
                    IF(
                        STRSTARTS(STR(?has_unit), "http://purl.obolibrary.org/obo/ECOSIM_"),
                        CONCAT("ECOSIM:", REPLACE(STR(?has_unit), "^.*ECOSIM_", "")),
                        ?raw_has_unit_label
                    )
                ) AS ?has_unit_label
            )
        }
    }
    OPTIONAL {
        ?class rdfs:subClassOf ?r2 .
        ?r2 a owl:Restriction ;
        owl:onProperty <http://purl.obolibrary.org/obo/ECOSIM_qualifier> ;
        owl:someValuesFrom ?qualifier .
        OPTIONAL {
            ?qualifier rdfs:label ?raw_qualifier_label 
            BIND(
                IF(
                    STRSTARTS(STR(?qualifier), "http://purl.obolibrary.org/obo/ECOSIMCONCEPT_"),
                    CONCAT("ECOSIMCONCEPT:", REPLACE(STR(?qualifier), "^.*ECOSIMCONCEPT_", "")),
                    IF(
                        STRSTARTS(STR(?qualifier), "http://purl.obolibrary.org/obo/ECOSIM_"),
                        CONCAT("ECOSIM:", REPLACE(STR(?qualifier), "^.*ECOSIM_", "")),
                        ?raw_qualifier_label
                    )
                ) AS ?qualifier_label
            )
        }
    }
    OPTIONAL {
        ?class rdfs:subClassOf ?r3 .
        ?r3 a owl:Restriction ;
        owl:onProperty <http://purl.obolibrary.org/obo/ECOSIM_attribute> ;
        owl:someValuesFrom ?attribute .
        OPTIONAL {
            ?attribute rdfs:label ?raw_attribute_label 
            BIND(
                IF(
                    STRSTARTS(STR(?attribute), "http://purl.obolibrary.org/obo/ECOSIMCONCEPT_"),
                    CONCAT("ECOSIMCONCEPT:", REPLACE(STR(?attribute), "^.*ECOSIMCONCEPT_", "")),
                    IF(
                        STRSTARTS(STR(?attribute), "http://purl.obolibrary.org/obo/ECOSIM_"),
                        CONCAT("ECOSIM:", REPLACE(STR(?attribute), "^.*ECOSIM_", "")),
                        ?raw_attribute_label
                    )
                ) AS ?attribute_label
            )
        }
    }
    OPTIONAL {
        ?class rdfs:subClassOf ?r4 .
        ?r4 a owl:Restriction ;
        owl:onProperty <http://purl.obolibrary.org/obo/ECOSIM#_measured_in> ;
        owl:someValuesFrom ?measured_in .
        OPTIONAL {
            ?measured_in rdfs:label ?raw_measured_in_label 
            BIND(
                IF(
                    STRSTARTS(STR(?measured_in), "http://purl.obolibrary.org/obo/ECOSIMCONCEPT_"),
                    CONCAT("ECOSIMCONCEPT:", REPLACE(STR(?measured_in), "^.*ECOSIMCONCEPT_", "")),
                    IF(
                        STRSTARTS(STR(?measured_in), "http://purl.obolibrary.org/obo/ECOSIM_"),
                        CONCAT("ECOSIM:", REPLACE(STR(?measured_in), "^.*ECOSIM_", "")),
                        ?raw_measured_in_label
                    )
                ) AS ?measured_in_label
            )
        }
    }
    OPTIONAL {
        ?class rdfs:subClassOf ?r5 .
        ?r5 a owl:Restriction ;
        owl:onProperty <http://purl.obolibrary.org/obo/ECOSIM#_measurement_of> ;
        owl:someValuesFrom ?measurement_of .
        OPTIONAL {
            ?measurement_of rdfs:label ?raw_measurement_of_label 
            BIND(
                IF(
                    STRSTARTS(STR(?measurement_of), "http://purl.obolibrary.org/obo/ECOSIMCONCEPT_"),
                    CONCAT("ECOSIMCONCEPT:", REPLACE(STR(?measurement_of), "^.*ECOSIMCONCEPT_", "")),
                    IF(
                        STRSTARTS(STR(?measurement_of), "http://purl.obolibrary.org/obo/ECOSIM_"),
                        CONCAT("ECOSIM:", REPLACE(STR(?measurement_of), "^.*ECOSIM_", "")),
                        ?raw_measurement_of_label
                    )
                ) AS ?measurement_of_label
            )
        }
    }
    OPTIONAL {
        ?class rdfs:subClassOf ?r6 .
        ?r6 a owl:Restriction ;
        owl:onProperty <http://purl.obolibrary.org/obo/ECOSIM_context> ;
        owl:someValuesFrom ?context .
        OPTIONAL {
            ?context rdfs:label ?raw_context_label 
            BIND(
                IF(
                    STRSTARTS(STR(?context), "http://purl.obolibrary.org/obo/ECOSIMCONCEPT_"),
                    CONCAT("ECOSIMCONCEPT:", REPLACE(STR(?context), "^.*ECOSIMCONCEPT_", "")),
                    IF(
                        STRSTARTS(STR(?context), "http://purl.obolibrary.org/obo/ECOSIM_"),
                        CONCAT("ECOSIM:", REPLACE(STR(?context), "^.*ECOSIM_", "")),
                        ?raw_context_label
                    )
                ) AS ?context_label
            )
        }
    }
    OPTIONAL {
        ?class rdfs:subClassOf ?parent .
        FILTER NOT EXISTS {
            ?parent a owl:Restriction .
        }
        BIND(
            IF(
                STRSTARTS(STR(?parent), "http://purl.obolibrary.org/obo/ECOSIMCONCEPT_"),
                CONCAT("ECOSIMCONCEPT:", REPLACE(STR(?parent), "^.*ECOSIMCONCEPT_", "")),
                IF(
                    STRSTARTS(STR(?parent), "http://purl.obolibrary.org/obo/ECOSIM_"),
                    CONCAT("ECOSIM:", REPLACE(STR(?parent), "^.*ECOSIM_", "")),
                    STR(?parent)
                )
            ) AS ?parent_label
        )
    }
}
GROUP BY ?class ?class_label
ORDER BY ?class_label